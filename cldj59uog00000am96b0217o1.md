# How to create Amazon EKS cluster in AWS cloud using Terraform

---

# **What is Amazon EKS?**

Amazon EKS is a fully managed container orchestration service. EKS allows you to quickly deploy a production-ready Kubernetes cluster in AWS, and deploy and manage containerized applications more easily with a fully managed Kubernetes service. Kubernetes is an open-source system for automating the deployment, scaling, and management of containerized applications.

![](https://1.bp.blogspot.com/-uQPotQWNG6A/YQyzyhAT59I/AAAAAAAADnA/Qy6RIntqWZUKs_NTO6RqPJRq08IFkI4aQCLcBGAsYHQ/s1400/EKS%2BCluster%2BArchitecture.png align="center")

<mark>EKS takes care of the master node/control plane. We need to create worker nodes.</mark>

**You can create an EKS cluster with the following node types:**

* Managed nodes -  Linux - Amazon EC2 instances
    
* Fargate - Serverless
    

<mark>We will learn how to create an EKS cluster based on Managed nodes (EC2 instances).</mark>

**EKS cluster can be created in the following different ways:**

1\. AWS console

2\. AWS CLI

3\. eksctl command

4\. <mark>using Terraform</mark>

<mark>We will create EKS cluster nodes using Terraform</mark>.

# **Pre-requisites:**

* **Install** [**Terraform**](https://biswajitblogs.hashnode.dev/how-to-install-terraform-on-ubuntu)
    
* **Install AWS CLI**
    
* [**Install kubectl**](https://biswajitblogs.hashnode.dev/how-to-create-eks-cluster-in-aws-using-eksctl)  – A command line tool for working with Kubernetes clusters.
    
* Create an IAM role with **<mark>an AdministratorAccess</mark>** policy or add AWS root keys
    
* Make sure you have already VPC created with at least two subnets.
    

**Create IAM Role with Administrator Access:**

<mark>You need to create an IAM role with&nbsp;the AdministratorAccess&nbsp;policy.<br>Go to the AWS console, IAM, and click on Roles. create a role</mark>

![](https://1.bp.blogspot.com/-2OQmiUkIwiw/XxkW-PQpc5I/AAAAAAAAC40/QCIU4Rw60eYU3wi9qT8VTauqy0o8pBaVQCLcBGAsYHQ/s1600/iam%2Brole.png align="center")

<mark>Select AWS services, Click EC2, Click on Next permissions</mark>

![](https://1.bp.blogspot.com/-ouvQxbxiNDw/XxkW-Jl1hjI/AAAAAAAAC44/24MMBqVk7F8-yBMW7GSAu4cZsksrMVsowCLcBGAsYHQ/s1600/iam%2Brole-2.png align="center")

<mark>Now search for&nbsp;</mark>  **<mark>AdministratorAccess</mark>**  <mark>&nbsp;policy and click</mark>

![](https://blogger.googleusercontent.com/img/a/AVvXsEjjskRvkR5pnfaQROPCxN0DW8-MQ-NzdypH0hsViuyJrp6egV78HpuGoFa9y3TMyDAjSNxuAq89KRl-fHczbfDf8fjKisGadMa-564YDZdv-cSFnzExFhJh3xaa0RJtpr1rvFbxrZ7dKI16U7smh5VOP_Ew5OLe_t3vL5RUr1NJ9mq_sPD_KQB_bkJxTw=s1686 align="center")

Skip on creating a tag.  
Now give a role name and create it.

![](https://blogger.googleusercontent.com/img/a/AVvXsEhrOromOxIqjJTtjAbVCDoXh8xbetlHg0gK4oI14BYB_wKttkHachW85ftMtCIG68K8QnO1nrQkeySsO3g5PmbAnwFOn1ouBts-czYunXxXBhXYGSqAIwP8EgJ3nChj0ZN3oD_6ZGjdg_hCFE-Aw-DaMtvAPwRv_OsfCBJ-A49IlrdUuSejDer4gl7ddQ=s1228 align="center")

**Assign the role to the EC2 instance:**  
Go to the AWS console, click on EC2, select EC2 instance, and Choose Security.  
Click on Modify IAM Role

![](https://blogger.googleusercontent.com/img/a/AVvXsEhgADJdlBGm75-mFamibVPaUwisiLvhfcE37DfwmNzX-xlNnQvUbCaGHnhqJFPqd2QQp19DC8W24MMv3_dofLWEnACQ3668nV-qSJXVPauaYk4w4E6jFesXH1IIRSSGChE6YsfmsShvMjgfIeZtPT-WpyYcQ-rkW8WqUdIo7KLIuIpyaOl5w42qYJKxUw=s978 align="center")

<mark>Choose the role you have created from the dropdown.<br>Select the role and click on Apply.</mark>

![](https://blogger.googleusercontent.com/img/a/AVvXsEjo5gCH18JVD1kdo_4L0wV6Ea49gLJim9C7MXSv62dwB5SPIKqUxl_jQSwexMg3kctMVK1i6iicjuS76UAOZv1vR55BaHbz6wdrz2JDgw3Je8cMIz3KCDtd5WPJ8o4U5vedJCqiL4qEu3_KLcQVr36XPo81JFQfKoHebcP9f8148lJjqSSusv_QUXl4ZQ=s860 align="center")

---

# **Create Terraform files**

```bash
sudo vi variables.tf
```

```json
 variable "subnet_id_1" {
  type = string
  default = "subnet-ec90408a"
 }

 variable "subnet_id_2" {
  type = string
  default = "subnet-0a911b04"
 }

 variable "cluster_name" {
  type = string
  default = "my-eks-cluster"
 }
```

```bash
sudo vi main.tf
```

```json
terraform {
 required_providers {
  aws = {
   source = "hashicorp/aws"
  }
 }
}

resource "aws_iam_role" "eks-iam-role" {
 name = "devops-eks-iam-role"

 path = "/"

 assume_role_policy = <<EOF
{
 "Version": "2012-10-17",
 "Statement": [
  {
   "Effect": "Allow",
   "Principal": {
    "Service": "eks.amazonaws.com"
   },
   "Action": "sts:AssumeRole"
  }
 ]
}
EOF

}

resource "aws_iam_role_policy_attachment" "AmazonEKSClusterPolicy" {
 policy_arn = "arn:aws:iam::aws:policy/AmazonEKSClusterPolicy"
 role    = aws_iam_role.eks-iam-role.name
}
resource "aws_iam_role_policy_attachment" "AmazonEC2ContainerRegistryReadOnly-EKS" {
 policy_arn = "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryReadOnly"
 role    = aws_iam_role.eks-iam-role.name
}

resource "aws_eks_cluster" "my-eks" {
 name = var.cluster_name
 role_arn = aws_iam_role.eks-iam-role.arn

 vpc_config {
  subnet_ids = [var.subnet_id_1, var.subnet_id_2]
 }

 depends_on = [
  aws_iam_role.eks-iam-role,
 ]
}

resource "aws_iam_role" "workernodes" {
  name = "eks-node-group-example"

  assume_role_policy = jsonencode({
   Statement = [{
    Action = "sts:AssumeRole"
    Effect = "Allow"
    Principal = {
     Service = "ec2.amazonaws.com"
    }
   }]
   Version = "2012-10-17"
  })
 }

 resource "aws_iam_role_policy_attachment" "AmazonEKSWorkerNodePolicy" {
  policy_arn = "arn:aws:iam::aws:policy/AmazonEKSWorkerNodePolicy"
  role    = aws_iam_role.workernodes.name
 }

 resource "aws_iam_role_policy_attachment" "AmazonEKS_CNI_Policy" {
  policy_arn = "arn:aws:iam::aws:policy/AmazonEKS_CNI_Policy"
  role    = aws_iam_role.workernodes.name
 }

 resource "aws_iam_role_policy_attachment" "EC2InstanceProfileForImageBuilderECRContainerBuilds" {
  policy_arn = "arn:aws:iam::aws:policy/EC2InstanceProfileForImageBuilderECRContainerBuilds"
  role    = aws_iam_role.workernodes.name
 }

 resource "aws_iam_role_policy_attachment" "AmazonEC2ContainerRegistryReadOnly" {
  policy_arn = "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryReadOnly"
  role    = aws_iam_role.workernodes.name
 }

 resource "aws_eks_node_group" "worker-node-group" {
  cluster_name  = aws_eks_cluster.my-eks.name
  node_group_name = "my-workernodes"
  node_role_arn  = aws_iam_role.workernodes.arn
  subnet_ids   = [var.subnet_id_1, var.subnet_id_2]
  instance_types = ["t2.medium"]

  scaling_config {
   desired_size = 2
   max_size   = 2
   min_size   = 1
  }

  depends_on = [
   aws_iam_role_policy_attachment.AmazonEKSWorkerNodePolicy,
   aws_iam_role_policy_attachment.AmazonEKS_CNI_Policy,
   aws_iam_role_policy_attachment.AmazonEC2ContainerRegistryReadOnly,
  ]
 }
```

### **Create EKS Cluster with two worker nodes using Terraform**

<mark>Now execute the below command:</mark>

```bash
terraform init
```

<mark>This will&nbsp;initialize&nbsp;terraform working directory.</mark>

![](https://1.bp.blogspot.com/-jpncNag0bQ0/XoYyKNXYKFI/AAAAAAAABrE/LcjGZffvBxcpyimBwGT5pMtBy5v2l0jkgCLcBGAsYHQ/s1600/init.png align="center")

<mark>Execute the below command:</mark>

```bash
terraform plan
```

<mark>The above command will show how many resources will be added.</mark>

**<mark>Plan: 10 to add, 0 to change, 0 to destroy.</mark>**

---

# Now let's create the EKS cluster

```bash
terraform apply
```

![](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEiP73oFy8iYWaqHab5pszE6TgJJWEoitUPi8NNnJXN8FK4aGn8lJGEq4T7Vz76kh1iKniZJA7mRbwWc_wOQVnCz8czbcb2g8RV-H1SJrxufyADvk4If_SrQOng9w8Cejbhyujxp8LZWlLFlvMIoOkR6nHuNpBNmvJlJiSl5clFazWlLFjwQcwIbhpNW/s1606/Screen%20Shot%202022-08-10%20at%209.10.33%20AM.png align="center")

<mark>This will create 10 resources.</mark>

### **Update Kube config**

<mark>Update the Kube config by entering the below command:</mark>

```bash
aws eks update-kubeconfig --name my-eks-cluster --region us-east-1
```

kubeconfig file is updated under **<mark>/home/ubuntu/.kube</mark>** folder.

<mark>you can view the kubeconfig file by entering the below command:</mark>

```bash
cat  /home/ubuntu/.kube/config
```

### **Connect to EKS cluster using kubectl commands**

To view the list of worker nodes as part of EKS cluster.

```bash
kubectl get nodes
```

![](https://1.bp.blogspot.com/-8tpK_USt5_Q/X4ZPAONKEDI/AAAAAAAADFw/fzrvmYdOCX8mNl5dwJnIWCFT5_od-ftkQCLcBGAsYHQ/s1288/get%2Bnodes.png align="center")

**Deploy Nginx on a Kubernetes Cluster:**  
<mark>Let us run some apps to make sure they are deployed to the Kubernetes cluster.</mark>

<mark>The below command will create deployment:</mark>

```bash
kubectl create deployment nginx --image=nginx
```

**View Deployments:**

```bash
kubectl get deployments
```

![](https://1.bp.blogspot.com/-4WMSu9MbTA0/Xt6GubuQY2I/AAAAAAAACe4/MtqK4HgSPMASBJXX2jxmIzyLYK-FpcvbwCLcBGAsYHQ/s1600/deploy%2B1.png align="center")

---

# **Delete EKS Cluster**

```bash
terraform destroy
```

<mark>the above command should delete the EKS cluster in AWS, it might take a few mins to clean up the cluster.</mark>

**OR**

**<mark>you can also delete the cluster under&nbsp;AWS console --&gt;&nbsp;Elastic Kubernetes Service --&gt; Clusters</mark>**

**<mark>Click on Delete cluster</mark>**

---